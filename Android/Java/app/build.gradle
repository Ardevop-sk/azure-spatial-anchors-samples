plugins {
    id "org.openapi.generator" version "5.1.1"
}

apply plugin: 'com.android.application'

def azureSpatialAnchorsSdkVersion = '2.9.0'

openApiValidate {
    inputSpec = "$rootDir/app/src/main/openapi/spatial-anchors-sharing-service.yaml"
    recommend = true
}
openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/app/src/main/openapi/spatial-anchors-sharing-service.yaml"
    outputDir = "$buildDir/generated/openapi"
    groupId = "$project.group"
    id = "$project.name-openapi"
    version = "$project.version"
    apiPackage = "com.example.mypackage.api"
    invokerPackage = "com.example.mypackage.invoker"
    modelPackage = "com.example.mypackage.model"
    configOptions = [
            language   : "android",
            java8      : "true",
            dateLibrary: "java8",
            library    : "retrofit2"
    ]
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.microsoft.sampleandroid"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        ignoreWarnings false
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'build/generated/openapi/src/main/java']
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //noinspection GradleDependency
    implementation 'com.google.ar:core:1.11.0'
    //noinspection GradleDependency
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.11.0'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation "com.microsoft.azure.spatialanchors:spatialanchors_jni:[${azureSpatialAnchorsSdkVersion}]"
    implementation "com.microsoft.azure.spatialanchors:spatialanchors_java:[${azureSpatialAnchorsSdkVersion}]"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'io.gsonfire:gson-fire:1.9.0-alpha2'
    implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.0'
    implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.common:1.0.0'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.swagger:swagger-annotations:1.6.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // support AAD user token scenario
    implementation 'com.microsoft.aad:adal:1.16.0'

    // Obj - a simple Wavefront OBJ file loader
    // https://github.com/javagl/Obj
    implementation 'de.javagl:obj:0.2.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
